plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.12.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

ext {
    set('springCloudVersion', "Hoxton.SR12")
    set('springCloudAlibabaVersion', "2.2.5.RELEASE")
    set('lombokVersion', '1.18.20')
    set('mysqlVersion', '8.0.25')
    set('apacheCommonVersion', '3.12.0')
    set('jwtVersion', '0.9.1')
    set('jaxbVersion', '2.3.0')
    set('activationVersion', '1.1.1')
}

repositories {
    mavenCentral()
}

// 子项目的通用配置
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = "com.flz.cloud.gw"
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = '11'
    jar.enabled = true

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    dependencyManagement {
        // 导入SpringBoot和SpringCloud依赖Bom
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }

        // 自定义配置第三方依赖
        dependencies {
            dependency "org.apache.commons:commons-lang3:${apacheCommonVersion}"
            dependency "mysql:mysql-connector-java:${mysqlVersion}"
            dependency "javax.xml.bind:jaxb-api:${jaxbVersion}"
            dependency "com.sun.xml.bind:jaxb-impl:${jaxbVersion}"
            dependency "com.sun.xml.bind:jaxb-core:${jaxbVersion}"
            dependency "javax.activation:activation:${activationVersion}"
            dependency "io.jsonwebtoken:jjwt:${jwtVersion}"
        }
    }
}
